// see https://github.com/nilsmagnus/wsdl2java
buildscript{
    repositories{
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'no.nils:wsdl2java:0.10'
    }
}
apply plugin: 'no.nils.wsdl2java'

description = "Core implementation of IEC 62325-504 technical specification. eemws-core includes schemas, wsdl, and compiled classes necessary for the eem web services"

wsdl2java {
    generatedWsdlDir = file("$projectDir/generated-sources")  // target directory for generated source coude
    wsdlDir = file("$projectDir/src/main/resources/wsdl") // define to support incremental build
    wsdlsToGenerate = [   //  2d-array of wsdls and cxf-parameters
                ['-validate',
                 '-wsdlLocation', 'file://urn-iec62325-504-wss-1-0.wsdl',
                 '-b',"$projectDir/src/main/resources/jaxws/bindings.xjc",
                 "$projectDir/src/main/resources/wsdl/urn-iec62325-504-wss-1-0.wsdl"]
        ]
    locale = Locale.ENGLISH
}

wsdl2javaExt {
    deleteGeneratedSourcesOnClean = true
    cxfVersion = "3.1.6"  // remove to use the latest version
}

jar {
    manifest {
        attributes 'Implementation-Title': project.description,
                   'Implementation-Version': version
    }
}

 modifyPom {
    project {
         name project.name
         description project.description
     }
  }